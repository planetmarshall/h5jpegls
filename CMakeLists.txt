cmake_minimum_required(VERSION 3.15)
project(h5jpegls 
    VERSION 1.0.0
    HOMEPAGE_URL https://github.com/planetmarshall/jpegls-hdf-filter
    DESCRIPTION "JPEG-LS Compression Filter"
    LANGUAGES CXX)

option(H5JPEGLS_BUILD_LIBRARY "Build as a library to be linked into an application instead of as a dynamic plugin")
option(H5JPEGLS_BUILD_TESTS "Build the unit tests" ON)
option(H5JPEGLS_COMPILE_STRICT "Treat compiler warnings as errors" ON)

if (H5JPEGLS_BUILD_LIBRARY)
    if (BUILD_SHARED_LIBS)
        message(STATUS "H5JPEGLS: Building Plugin as library (Shared)")
    else()
        message(STATUS "H5JPEGLS: Building Plugin as library (Static)")
    endif()
else()
    message(STATUS "H5JPEGLS: Building Plugin as dynamic module")
endif()

if (EXISTS "${CMAKE_BINARY_DIR}/conanbuildinfo.cmake")
    include(${CMAKE_BINARY_DIR}/conanbuildinfo.cmake)
    conan_basic_setup(KEEP_RPATHS)
    list(PREPEND CMAKE_MODULE_PATH ${CMAKE_BINARY_DIR})
endif()

list(PREPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_SOURCE_DIR}/cmake)
include(h5jpegls_version)
h5jpegls_configure_version_file(${CMAKE_CURRENT_SOURCE_DIR}/src/version.hpp.in ${CMAKE_CURRENT_SOURCE_DIR}/src/version.hpp)

if (MSVC)
    add_compile_options(/W4 /permissive-)
    if (H5JPEGLS_COMPILE_STRICT)
        add_compile_options(/WX)
    endif()
else()
    add_compile_options(-Wall -Wpedantic -Wextra -Wno-unknown-pragmas)
    if (CMAKE_CXX_COMPILER_ID MATCHES Clang$)
        add_compile_options(-Wconversion -Wconditional-uninitialized)
    endif()
    if (H5JPEGLS_COMPILE_STRICT)
        add_compile_options(-Werror)
    endif()
endif()

find_package(charls REQUIRED)
find_package(HDF5 REQUIRED COMPONENTS C)
find_package(TBB REQUIRED)
add_subdirectory(src)

if (H5JPEGLS_BUILD_TESTS)
    find_package(Catch2 QUIET)
    if(NOT Catch2_FOUND)
        message(WARNING "Catch2 not found - C++ tests will not be available")
    else()
        enable_testing()
        add_subdirectory(test)
    endif()
endif()

set(CPACK_PACKAGE_VENDOR "Andrew Marshall")
set(CPACK_PACKAGE_VERSION ${H5JPEGLS_VERSION})
set(CPACK_PACKAGE_CONTACT "planetmarshalluk@gmail.com")
set(CPACK_PACKAGE_DESCRIPTION "h5jpegls Compression Filter Plugin for HDF5")
set(CPACK_RESOURCE_FILE_LICENSE "${CMAKE_CURRENT_SOURCE_DIR}/LICENSE.txt")
set(CPACK_RESOURCE_FILE_README "${CMAKE_CURRENT_SOURCE_DIR}/README.md")
set(CPACK_PACKAGE_INSTALL_DIRECTORY "")
if (WIN32)
    file(TO_CMAKE_PATH "$ENV{ALLUSERSPROFILE}" ALL_USERS_PROFILE)
    # See https://gitlab.kitware.com/cmake/cmake/-/issues/22441
    string(REPLACE "/" "\\\\" ALL_USERS_PROFILE "${ALL_USERS_PROFILE}")
    set(CPACK_NSIS_INSTALL_ROOT "${ALL_USERS_PROFILE}")
    set(CPACK_NSIS_PACKAGE_NAME ${PROJECT_NAME})
elseif(NOT CPACK_PACKAGING_INSTALL_PREFIX)
    set(CPACK_PACKAGING_INSTALL_PREFIX "/usr/local")
endif()
include(CPack)
